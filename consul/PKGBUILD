# Maintainer: Joe Harrison <joehazzers@gmail.com>
pkgname=('consul' 'consul-ui')
pkgbase="consul"
pkgver=0.4.0
pkgrel=1
pkgdesc="Service discovery and configuration made easy. Distrubuted, highly available, and datacenter-aware."
arch=('x86_64')
url="http://consu.io"
license=('custom:MPLv2')
makedepends=('git' 'go' 'ruby-sass' 'ruby-uglifier')
source=("$pkgbase::git+https://github.com/hashicorp/consul.git#tag=v$pkgver")
sha256sums=('SKIP')
_pkgpath="github.com/hashicorp/consul"

prepare() {
  cd "$srcdir"
  rm -rf "src/$_pkgpath"
  mkdir -p $(dirname "src/$_pkgpath")
  mv "$pkgbase" "src/$_pkgpath"
}

build() {
  # Compile consul
  export GOPATH="$srcdir"

  cd "$srcdir/src/$_pkgpath"

  make

  # Compile consul-ui
  cd "$srcdir/src/$_pkgpath/ui"

  make dist
}

package_consul() {
  depends=('glibc')

  cd "$srcdir/src/$_pkgpath"

  install -Dm755 "bin/consul" "$pkgdir/usr/bin/consul"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_consul-ui() {
  pkgdesc="Web UI files for Consul"
  arch=('any')
  optdepends=('consul: intended to serve the ui files')

  cd "$srcdir/src/$_pkgpath"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$srcdir/src/$_pkgpath/ui"

  install -Ddm755 dist/ "$pkgdir/usr/share/consul/ui"
  install -Dm644 dist/index.html "$pkgdir/usr/share/consul/ui/index.html"
  install -Ddm755 dist/static/ "$pkgdir/usr/share/consul/ui/static"
  install -Dm644 dist/static/* "$pkgdir/usr/share/consul/ui/static"
}

